
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.15'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

def grpcVersion = '1.37.0'
mainClassName = 'itx.examples.grpc.service.Main'

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'com.beust:jcommander:1.78'

    implementation "com.google.api.grpc:proto-google-common-protos:2.1.0"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation 'org.testng:testng:7.3.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.15.8'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
        }
    }
}

idea {
    module {
        // Not using generatedSourceDirs because of
        // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java");
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc");
    }
}